def appName = "GiddyApp"
def releaseName = "AppleJack"

android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        outputFileName = "${appName}-${variant.versionName}.apk"
    }
}
android.defaultConfig {
    setProperty("archivesBaseName", "${appName}-${versionName}")
}

ext.branchName = { ->
    return releaseName
}

ext.generateVersionCode = { ->
    return (int) (((new Date().getTime() / 1000) - 1546544501) / 10)
}

ext.gitTag = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        return "v" + stdout.toString().trim()
    } catch (Throwable ignore) {
        return "local"
    }
}

ext.gitCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=6', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

ext.generateVersionName = { ->
    return "${gitTag()}-${branchName()}-${gitCommit()}"
}

task showBundleName {
    doLast {
        println "${appName}-${android.defaultConfig.versionName}.aab"
    }
}