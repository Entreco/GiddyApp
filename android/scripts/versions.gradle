ext {
    appName = "GiddyApp"
    releaseName = "AppleJack"

    kotlinVersion = '1.3.30'
    gradleVersion = '3.4.0'
    gmsVersion = '4.2.0'

    compileSdkVersion = 28
    minSdkVersion = 24
    targetSdkVersion = 28

    dependencies = [
            dagger = '2.22.1',
            material = '1.1.0-alpha05',
            playCore = '1.4.1',
            playInstant = '16.1.2',
            xLifecycle = '2.0.0',
            xAppcompat = '1.0.2',
            xArchitecture = '2.0.0',
            xConstraintLayout = '2.0.0-alpha4',
            xDynamicAnimation = '1.0.0',
            xLegacyCore = '1.0.0',
            xPalette = '1.0.0',
            fbCore = '16.0.8',
            fbCommon = '16.0.4',
            fbFireStore = '18.2.0',
            fbStorage = '16.1.0',
            xExifInterface = '1.0.0',
            cropImageView = '2.8.0',
            glide = '4.9.0',
            mlVision = '19.0.3',
            mlVisionImageLabelModel = '17.0.2',

            // UnitTest
            junit = '4.12',
            mockito = '2.1.0',

            // InstrumentationTest
            dexmaker = '2.19.1',
            xTestCore = '1.0.0',
            xTestRules = '1.1.0',
            xTestRunner = '1.1.0',
            xTestExtJunit = '1.1.0',
            xEspressoCore = '3.1.0',

            // Development tools
            leakCanary = '1.6.3'
    ]
}

ext.branchName = { ->
    return releaseName
}

ext.generateVersionCode = { ->
    return (int) (((new Date().getTime() / 1000) - 1546544501) / 10)
}

ext.gitTag = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        return "v" + stdout.toString().trim()
    } catch (Throwable ignore) {
        return "local"
    }
}

ext.gitCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=6', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

ext.generateVersionName = { ->
    return "${gitTag()}-${branchName()}-${gitCommit()}"
}

configurations.all {
    resolutionStrategy {
        force "androidx.appcompat:appcompat:$xAppcompat"
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    }
}