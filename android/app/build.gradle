apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "nl.entreco.giddyapp"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        setProperty("archivesBaseName", "${appName}-${versionName}")

        resValue "string", "version", "\"${appName}-${versionName}\""

        resConfigs "en", "nl"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            // There's a Dex Splitter issue when enabling DataBinding & proguard in dynamic features
            // The temporary workaround is to disable shrinking
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../proguard-rules.pro'
        }
    }

    //Add the following configuration in order to target Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    dynamicFeatures = [":features:launcher", ":features:creator", ":features:viewer", ":instant"]


}

android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        outputFileName = "${appName}-${variant.versionName}.apk"
    }
}

dependencies {
    api project(':libraries:libCore')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"

    implementation "androidx.appcompat:appcompat:$xAppcompat"
    implementation "androidx.constraintlayout:constraintlayout:$xConstraintLayout"
    implementation "com.google.firebase:firebase-core:$fbCore"

    testImplementation "junit:junit:$junit"
}

task showBundleName {
    doLast {
        println "${appName}-${android.defaultConfig.versionName}.aab"
    }
}

apply plugin: 'com.google.gms.google-services'
